# vim: set ft=zsh expandtab:

# Browse to the URL of $1, or the 1st browseable configured remote URL
# Set $REPLY as a side effect
# Find the URL, convert to HTTPS, open with $BROWSER

gitlab() {
	zmodload zsh/regex
    local _status
    git status >/dev/null 2>&1 
    _status=$?

    if (( _status == 128 )); then
        1>&2 print "$0: Not in a Git repository"
        return $_status
    elif (( _status != 0 )); then
        1>&2 print "$0: git status returned $_status, how did this happen?"
        return $_status
    else
        local -a remotes
        remotes=($1 $(git remote))
        if (( $#remotes == 0 )); then
            1>&2 print "$0: No remote repos to browse to"
            return 1
        fi

        for remote in $remotes; do 
            local URL=$(git remote get-url $remotes[1])
            if [[ $URL -regex-match ^(git@[^:]+:[^/]+/[^/ ]+) ]]; then
                URL=$MATCH
                URL=${URL/:/\/}
                URL=${URL/git@/https:\/\/}
                break
            elif [[ $URL -regex-match ^(https://[^/]+/[^/]+) ]]; then
                break
            else
                1>&2 print "Skipping unbrowseable remote $remote '$URL'"
                unset URL
            fi
        done

		REPLY=$URL
        if [[ -z $URL ]]; then
            1>&2 print "There are no browseable remote URLs"
            return 2
        fi

        if [[ -n $BROWSER ]]; then
            print $BROWSER $URL
            $BROWSER $URL 2>/dev/null
        else
            print Browse to $URL
        fi
	fi
}
